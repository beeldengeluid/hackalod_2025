#+ summary: Query to get all triples (dates, points) from Wikidata for Muziekweb preformers. Note 'mw_id_value' needs to be replaced.
#+ endpoint: https://query.wikidata.org/sparql
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
CONSTRUCT {
    ?s wdt:P5882 ?mw_id .
    ?s rdfs:label ?sLabel .
    ?s schema:description ?sDescription .
    ?s skos:altLabel ?sAltLabel .
    # ?s wdt:P31 ?instance_of .
    ?instance_of rdfs:label ?instance_ofLabel .
    ?s wdt:P21 ?gender .
    ?gender rdfs:label ?genderLabel .
    ?s wdt:P569 ?birthDate .
    ?s wdt:P570 ?deathDate .
    ?s wdt:P19 ?placeOfBirth .
    ?placeOfBirth rdfs:label ?placeOfBirthLabel .
    ?placeOfBirth wdt:P625 ?_coordinatesPoB .
    ?s wdt:P20 ?placeOfDeath .
    ?placeOfDeath rdfs:label ?placeOfDeathLabel .
    ?placeOfDeath wdt:P625 ?_coordinatesPoD .
    ?s wdt:P27 ?nationality .
    ?nationality rdfs:label ?nationalityLabel .
    ?s wdt:P18 ?imageUrl 
    }
WHERE {
    VALUES ?mw_id { "mw_id_value" }
    ?s wdt:P5882 ?mw_id .
    # OPTIONAL{?s wdt:P31 ?instance_of }
    OPTIONAL{?s wdt:P21 ?gender }
        OPTIONAL{ ?s wdt:P569 ?born
            BIND(
            COALESCE(
                IF(
                (?born < "+1-00-00T00:00:00Z"^^xsd:dateTime),
                CONCAT(STR(1+YEAR(?born)*-1), " BC"),
                1/0),
                xsd:date(?born)
            )
            AS ?birthDate
            )
        }
    OPTIONAL{?s wdt:P570 ?dateOfDeath BIND (
        xsd:date(?dateOfDeath) AS ?deathDate
        ) }
    OPTIONAL{ ?s wdt:P19 ?placeOfBirth }
    OPTIONAL{ ?s wdt:P19/wdt:P625 ?_coordinatesPoB }
    OPTIONAL{?s wdt:P20 ?placeOfDeath }
    OPTIONAL{ ?s wdt:P18/wdt:P625 ?_coordinatesPoD }
    OPTIONAL{?s wdt:P27 ?nationality }
    OPTIONAL{?s wdt:P18 ?imageUrl }
    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .
    }
}